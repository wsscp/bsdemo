CREATE OR REPLACE 
PROCEDURE "SPLIT_CUSTOMER_ORDER" (ORDER_ID IN VARCHAR2, TARGET_COUNT IN NUMBER, UPDATOR IN VARCHAR2, RESULT_ORDERS OUT SYS_REFCURSOR)
AS
	SALES_ORDER_ID T_PLA_CUSTOMER_ORDER.SALES_ORDER_ID%TYPE;
	REMARKS T_PLA_CUSTOMER_ORDER.REMARKS%TYPE;
	ORG_CODE T_PLA_CUSTOMER_ORDER.ORG_CODE%TYPE;
	SHCEDULE_ORDER T_PLA_CUSTOMER_ORDER.SHCEDULE_ORDER%TYPE;
	TEMP_ORDER_ID T_PLA_CUSTOMER_ORDER."ID"%TYPE;
	TEMP_ORDER_LENGTH T_PLA_CUSTOMER_ORDER_ITEM.ORDER_LENGTH%TYPE;
	TEMP_DEC_LENGTH T_PLA_CUSTOMER_ORDER_ITEM_DEC."LENGTH"%TYPE; -- 当前要用的长度
	TEMP_MIN_LENGTH T_PLA_CUSTOMER_ORDER_ITEM_DEC."LENGTH"%TYPE;
	TEMP_STANDARD_LENGTH T_ORD_SALES_ORDER_ITEM.STANDARD_LENGTH%TYPE;
	TEMP_ORDER_ITEM_ID T_PLA_CUSTOMER_ORDER_ITEM."ID"%TYPE;
	NOW CONSTANT TIMESTAMP:=SYSDATE;
	TO_CONFIRM CONSTANT VARCHAR2(15):='TO_CONFIRM';	
	TO_DO CONSTANT VARCHAR2(10):='TO_DO';
	TO_AUDIT CONSTANT VARCHAR2(10):='TO_AUDIT';
	CURSOR LENGTHS IS SELECT c."ID",c.ORDER_LENGTH, s.STANDARD_LENGTH, 
		REGEXP_SUBSTR(REGEXP_SUBSTR(s.LENGTH_CONSTRAINTS,',[^:]+:0'),'[^,:]+')
		FROM T_PLA_CUSTOMER_ORDER_ITEM c, T_ORD_SALES_ORDER_ITEM s
	WHERE c.CUSTOMER_ORDER_ID = ORDER_ID AND s."ID" = c.SALES_ORDER_ITEM_ID;
BEGIN
	-- 初始化变量
	SELECT SALES_ORDER_ID,ORG_CODE,REMARKS,SHCEDULE_ORDER INTO SALES_ORDER_ID,ORG_CODE,REMARKS,SHCEDULE_ORDER 
		FROM T_PLA_CUSTOMER_ORDER 
	WHERE "ID" = ORDER_ID;
	INSERT INTO TEMP_STRING(STR) VALUES(ORDER_ID);

	-- 更新原有customerOrderItem
	UPDATE T_PLA_CUSTOMER_ORDER_ITEM
		SET ORDER_LENGTH = ORDER_LENGTH/TARGET_COUNT, MODIFY_USER_CODE = UPDATOR, MODIFY_TIME = NOW
	WHERE CUSTOMER_ORDER_ID = ORDER_ID;

	-- 删除原有OrderTask
	DELETE FROM T_PLA_ORDER_TASK t WHERE EXISTS (
		SELECT t."ID" FROM T_PLA_CUSTOMER_ORDER_ITEM i, T_PLA_CUSTOMER_ORDER_ITEM_DEC d, T_PLA_CU_ORDER_ITEM_PRO_DEC p
		WHERE t.ORDER_ITEM_PRO_DEC_ID = p."ID" AND p.ORDER_ITEM_DEC_ID = d."ID" 
			AND d.ORDER_ITEM_ID = i."ID" AND i.CUSTOMER_ORDER_ID = ORDER_ID
	);

	-- 删除原有CustomerOrderItemProDec
	DELETE FROM T_PLA_CU_ORDER_ITEM_PRO_DEC p WHERE EXISTS (
		SELECT p."ID" FROM T_PLA_CUSTOMER_ORDER_ITEM i, T_PLA_CUSTOMER_ORDER_ITEM_DEC d
		WHERE p.ORDER_ITEM_DEC_ID = d."ID" AND d.ORDER_ITEM_ID = i."ID" AND i.CUSTOMER_ORDER_ID = ORDER_ID
	);

	-- 删除原有CustomerOrderItemDec
	DELETE FROM T_PLA_CUSTOMER_ORDER_ITEM_DEC d WHERE EXISTS (
		SELECT i."ID" FROM T_PLA_CUSTOMER_ORDER_ITEM i
		WHERE d.ORDER_ITEM_ID = i."ID" AND i.CUSTOMER_ORDER_ID = ORDER_ID
	);

	-- 重建CustomerOrderItemDec
	OPEN LENGTHS;
	LOOP
	FETCH LENGTHS INTO TEMP_ORDER_ITEM_ID, TEMP_ORDER_LENGTH, TEMP_STANDARD_LENGTH, TEMP_MIN_LENGTH;
	EXIT WHEN LENGTHS%NOTFOUND;
		LOOP
			IF TEMP_ORDER_LENGTH - TEMP_STANDARD_LENGTH > TEMP_MIN_LENGTH THEN 
				-- 可以分出一卷
				TEMP_DEC_LENGTH := TEMP_STANDARD_LENGTH;
				TEMP_ORDER_LENGTH := TEMP_ORDER_LENGTH - TEMP_STANDARD_LENGTH;
			ELSE
				-- 不能再分出一卷
				TEMP_DEC_LENGTH := TEMP_ORDER_LENGTH;
				TEMP_ORDER_LENGTH := 0;
			END IF;
			INSERT INTO T_PLA_CUSTOMER_ORDER_ITEM_DEC(
				"ID", ORDER_ITEM_ID, "LENGTH", STATUS, CREATE_USER_CODE, CREATE_TIME, MODIFY_USER_CODE, MODIFY_TIME
			) VALUES (SYS_GUID(),TEMP_ORDER_ITEM_ID, TEMP_ORDER_LENGTH, TO_DO, UPDATOR, NOW, UPDATOR, NOW);
			EXIT WHEN TEMP_ORDER_LENGTH = 0;
		END LOOP;
	END LOOP;
	CLOSE LENGTHS;

	FOR v_index IN 2..TARGET_COUNT LOOP
		TEMP_ORDER_ID:=SYS_GUID();
		INSERT INTO TEMP_STRING(STR) VALUES(TEMP_ORDER_ID);
		-- 创建CustomerOrder count-1份
		INSERT INTO T_PLA_CUSTOMER_ORDER(
			"ID", SALES_ORDER_ID, REMARKS, SHCEDULE_ORDER, STATUS, ORG_CODE, CREATE_USER_CODE, CREATE_TIME, MODIFY_USER_CODE, MODIFY_TIME
		) VALUES(TEMP_ORDER_ID, SALES_ORDER_ID, REMARKS, SHCEDULE_ORDER, TO_CONFIRM, ORG_CODE, UPDATOR, NOW, UPDATOR, NOW);
		
		-- 根据原CustomerOrderItem创建CustomerOrderItem count-1份
		INSERT INTO T_PLA_CUSTOMER_ORDER_ITEM(
			"ID", CUSTOMER_ORDER_ID, PRODUCT_CODE, ORDER_LENGTH, SALES_ORDER_ITEM_ID, REMARKS, STATUS,
			CREATE_USER_CODE, CREATE_TIME, MODIFY_USER_CODE, MODIFY_TIME, CRAFTS_ID
		) SELECT SYS_GUID(), TEMP_ORDER_ID, i.PRODUCT_CODE, i.ORDER_LENGTH, i.SALES_ORDER_ITEM_ID, i.REMARKS, i.CRAFTS_ID, TO_DO,
				UPDATOR, NOW, UPDATOR, NOW 
			FROM T_PLA_CUSTOMER_ORDER_ITEM i
		WHERE i.CUSTOMER_ORDER_ID = ORDER_ID;

		-- 创建CustomerOrderItemDec count-1份
		INSERT INTO T_PLA_CUSTOMER_ORDER_ITEM_DEC(
			"ID", ORDER_ITEM_ID, "LENGTH", STATUS, CREATE_USER_CODE, CREATE_TIME, MODIFY_USER_CODE, MODIFY_TIME
		) SELECT SYS_GUID(),i."ID", d."LENGTH", TO_DO, UPDATOR, NOW, UPDATOR, NOW 
			FROM T_PLA_CUSTOMER_ORDER_ITEM i, T_PLA_CUSTOMER_ORDER_ITEM o, T_PLA_CUSTOMER_ORDER_ITEM_DEC d
		WHERE i.CUSTOMER_ORDER_ID = TEMP_ORDER_ID AND i.SALES_ORDER_ITEM_ID = o.SALES_ORDER_ITEM_ID 
			AND o.CUSTOMER_ORDER_ID = ORDER_ID AND d.ORDER_ITEM_ID = o."ID";
	END LOOP;
	
	--返回拆分后的订单
	OPEN RESULT_ORDERS FOR SELECT o."ID",o.CUSTOMER_ORDER_NO,o.CUSTOMER_OA_DATE,o.OA_DATE,o.STATUS,
		o.REMARKS,o.FIXED_OA,o.CONFIRM_DATE,o.SALES_ORDER_ID,o.SHCEDULE_ORDER,o.ORG_CODE,
		o.CREATE_USER_CODE,o.CREATE_TIME,o.MODIFY_USER_CODE,o.MODIFY_TIME
		FROM TEMP_STRING t, T_PLA_CUSTOMER_ORDER o
	WHERE t.STR = o."ID";
	
	DELETE FROM TEMP_STRING;

END;
